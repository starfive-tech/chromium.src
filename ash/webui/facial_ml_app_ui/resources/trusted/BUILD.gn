# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Facial ML App is ash-chrome only")

ts_library("trusted_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"

  in_files = [ "main.ts" ]

  tsconfig_base = "../../tsconfig_base.json"
  composite = true

  # Allows TSC to check the generated mojom-webui/ directory when it encounters
  # imports.
  path_mappings =
      [ "/*|" + rebase_path("$root_gen_dir/mojom-webui/*", target_gen_dir) ]
}

generate_grd("trusted_grd") {
  grd_prefix = "ash_facial_ml_app"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"

  input_files = [
    "app_icon_192.png",
    "app_icon_512.png",
    "index.html",
  ]
  input_files_base_dir = rebase_path(".", "//")

  # trusted_ts.manifest is generated by :trusted_ts and adds all of the in_files
  # to the grd.
  manifest_files =
      filter_include(get_target_outputs(":trusted_ts"), [ "*.manifest" ])

  deps = [ ":trusted_ts" ]
}
