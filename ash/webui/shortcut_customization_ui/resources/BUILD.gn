# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/css_to_wrapper.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash)

preprocess_folder = "preprocessed"

non_web_component_files = [
  "accelerator_lookup_manager.js",
  "fake_data.js",
  "fake_shortcut_provider.js",
  "mojo_interface_provider.js",
  "shortcut_types.js",
]

web_component_files = [
  "accelerator_edit_dialog.js",
  "accelerator_edit_view.js",
  "accelerator_row.js",
  "accelerator_subsection.js",
  "accelerator_view.js",
  "input_key.js",
  "shortcut_customization_app.js",
  "shortcut_input.js",
  "shortcuts_page.js",
]

html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".js", ".html") ]
}

icons_html_files = [ "icons.html" ]

html_wrapper_files = []
foreach(f, html_files + icons_html_files) {
  html_wrapper_files += [ f + ".js" ]
}

js_files = web_component_files + non_web_component_files

# Files that are passed as input to css_to_wrapper().
css_files = [
  "shortcut_customization_fonts.css",
  "shortcut_customization_shared.css",
]

# Files that are generated by css_to_wrapper().
css_wrapper_files = []
foreach(f, css_files) {
  css_wrapper_files += [ f + ".js" ]
}

static_resource_files = [
  "app_icon_192.png",
  "index.html",
]

css_to_wrapper("css_wrapper_files") {
  in_files = css_files
  use_js = true
}

html_to_wrapper("html_to_wrapper_files") {
  in_files = html_files + icons_html_files
  use_js = true
}

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = js_files
}

preprocess_if_expr("preprocess_generated") {
  deps = [
    ":css_wrapper_files",
    ":html_to_wrapper_files",
  ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = html_wrapper_files + css_wrapper_files
}

ts_library("build_ts") {
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = js_files + css_wrapper_files + html_wrapper_files

  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
  ]

  extra_deps = [
    ":preprocess",
    ":preprocess_generated",
    "//ash/webui/common/resources:copy_checked_in_dts_files",
  ]
}

generate_grd("build_grd") {
  input_files_base_dir = rebase_path(".", "//")
  input_files = static_resource_files
  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  grd_prefix = "ash_shortcut_customization_app"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}
