# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/buildflags.gni")
import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_wrapper.gni")

preprocess_folder = "preprocessed"

non_web_component_files = [ "inline_login_browser_proxy.js" ]

web_component_files = [ "inline_login_app.js" ]

if (is_chromeos_ash) {
  web_component_files += [
    "signin_blocked_by_policy_page.js",
    "signin_error_page.js",
    "welcome_page_app.js",
  ]
}

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".js", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".js" ]
}

group("components") {
  deps = [
    ":html_wrapper_files",
    ":preprocess",
    ":preprocess_generated",
  ]
}

preprocess_if_expr("preprocess") {
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = non_web_component_files + web_component_files
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":html_wrapper_files" ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = html_wrapper_files
}

js_type_check("closure_compile") {
  is_polymer3 = true
  closure_flags = default_closure_args + [
                    "js_module_root=" +
                        rebase_path("//chrome/browser/resources/inline_login/",
                                    root_build_dir),
                    "js_module_root=./gen/chrome/browser/resources/inline_login/",
                    "js_module_root=./gen/chrome/browser/resources/inline_login/preprocessed/",
                    "js_module_root=" +
                        rebase_path("//chrome/browser/resources/",
                                    root_build_dir),
                    "js_module_root=./gen/chrome/browser/resources/",
                  ]
  deps = [
    ":inline_login_app",
    ":inline_login_browser_proxy",
  ]
  if (is_chromeos_ash) {
    deps += [
      ":signin_blocked_by_policy_page",
      ":signin_error_page",
      ":welcome_page_app",
    ]
  }
}

js_library("inline_login_app") {
  sources = [ "$target_gen_dir/$preprocess_folder/inline_login_app.js" ]
  deps = [
    ":inline_login_browser_proxy",
    "//chrome/browser/resources/gaia_auth_host:authenticator.m",
    "//third_party/polymer/v3_0/components-chromium/paper-spinner:paper-spinner-lite",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:i18n_behavior",
    "//ui/webui/resources/cr_elements:web_ui_listener_behavior",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
  if (is_chromeos_ash) {
    deps += [
      ":signin_blocked_by_policy_page",
      ":signin_error_page",
      ":welcome_page_app",
      "//chrome/browser/resources/chromeos/arc_account_picker:arc_account_picker_app",
      "//chrome/browser/resources/chromeos/arc_account_picker:arc_util",
    ]
  }
  externs_list = [
    "$externs_path/webview_tag.js",
    "//ui/webui/resources/cr_elements/cr_view_manager/cr_view_manager_externs.js",
  ]
  extra_deps = [ ":preprocess" ]
}

js_library("welcome_page_app") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:i18n_behavior",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("signin_blocked_by_policy_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("signin_error_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("inline_login_browser_proxy") {
  sources =
      [ "$target_gen_dir/$preprocess_folder/inline_login_browser_proxy.js" ]
  deps = [
    "//chrome/browser/resources/gaia_auth_host:authenticator.m",
    "//ui/webui/resources/js:cr.m",
  ]
  externs_list = [ "$externs_path/chrome_send.js" ]
  extra_deps = [ ":preprocess" ]
}

html_to_wrapper("html_wrapper_files") {
  use_js = true
  in_files = html_files
}
