# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//crypto/features.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/css_to_wrapper.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")
import("certificate_manager.gni")

assert(use_nss_certs)

preprocess_folder_tmp = "$root_gen_dir/ui/webui/resources/preprocessed/cr_components/certificate_manager_tmp"
preprocess_folder = "$root_gen_dir/ui/webui/resources/preprocessed/cr_components/certificate_manager"

preprocess_if_expr("preprocess") {
  visibility = [
    ":build_ts",
    ":css_wrapper_files",
    ":html_wrapper_files",
  ]
  in_folder = "."
  out_folder = preprocess_folder_tmp
  in_files = ts_files + html_files + css_files
}

css_to_wrapper("css_wrapper_files") {
  deps = [ ":preprocess" ]
  in_folder = preprocess_folder_tmp
  out_folder = preprocess_folder_tmp
  in_files = css_files
  minify = optimize_webui
}

html_to_wrapper("html_wrapper_files") {
  deps = [ ":preprocess" ]
  in_folder = preprocess_folder_tmp
  out_folder = preprocess_folder_tmp
  in_files = html_files
  minify = optimize_webui
}

ts_library("build_ts") {
  root_dir = preprocess_folder_tmp
  out_dir = preprocess_folder
  composite = true
  tsconfig_base = "tsconfig_base.json"
  in_files = ts_files + css_wrapper_files + html_wrapper_files
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]

  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
  ]
  extra_deps = [
    ":css_wrapper_files",
    ":html_wrapper_files",
    ":preprocess",
  ]
}

generate_grd("build_grdp") {
  grd_prefix = "cr_components_certificate_manager"
  out_grd = "$target_gen_dir/resources.grdp"
  public_deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  resource_path_prefix = "cr_components/certificate_manager"
}
