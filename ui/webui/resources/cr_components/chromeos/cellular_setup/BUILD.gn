# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/html_to_js.gni")

assert(is_chromeos, "CellularSetup UI is Chrome OS only.")

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":activation_code_page",
    ":activation_verification_page",
    ":base_page",
    ":button_bar",
    ":cellular_eid_dialog",
    ":cellular_setup",
    ":cellular_setup_delegate",
    ":cellular_types",
    ":confirmation_code_page",
    ":esim_flow_ui",
    ":esim_manager_listener_behavior",
    ":esim_manager_utils",
    ":final_page",
    ":mojo_interface_provider",
    ":profile_discovery_list_item",
    ":profile_discovery_list_page",
    ":provisioning_page",
    ":psim_flow_ui",
    ":setup_loading_page",
    ":subflow_behavior",
    ":webview_post_util",
  ]
}

js_library("base_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:i18n_behavior",
  ]
}

js_library("final_page") {
  deps = [
    ":base_page",
    ":cellular_setup_delegate",
    "//ui/webui/resources/cr_elements:i18n_behavior",
  ]
}

js_library("activation_verification_page") {
  deps = [
    ":base_page",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:i18n_behavior",
  ]
  externs_list =
      [ "//ui/webui/resources/cr_elements/cr_lottie/cr_lottie_externs.js" ]
}

js_library("activation_code_page") {
  deps = [
    ":cellular_setup_delegate",
    ":subflow_behavior",
    "//third_party/polymer/v3_0/components-chromium/paper-spinner:paper-spinner-lite",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:i18n_behavior",
  ]
}

js_library("confirmation_code_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/paper-spinner:paper-spinner-lite",
    "//ui/webui/resources/cr_elements:i18n_behavior",
  ]
}

js_library("profile_discovery_list_item") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/paper-spinner:paper-spinner-lite",
    "//ui/webui/resources/cr_elements:i18n_behavior",
  ]
}

js_library("profile_discovery_list_page") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/iron-list:iron-list",
    "//ui/webui/resources/cr_elements:i18n_behavior",
  ]
}

js_library("mojo_interface_provider") {
  deps = [
    "//chromeos/ash/services/cellular_setup/public/mojom:mojom_js_library_for_compile",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("esim_manager_utils") {
  deps = [ ":mojo_interface_provider" ]
}

js_library("esim_manager_listener_behavior") {
  deps = [ ":mojo_interface_provider" ]
}

js_library("webview_post_util") {
  deps = []
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/webview_tag.js",
  ]
}

js_library("setup_loading_page") {
  deps = [
    ":base_page",
    ":cellular_setup_delegate",
    "//ui/webui/resources/js:assert.m",
  ]
  externs_list =
      [ "//ui/webui/resources/cr_elements/cr_lottie/cr_lottie_externs.js" ]
}

js_library("provisioning_page") {
  deps = [
    ":base_page",
    ":cellular_setup_delegate",
    ":webview_post_util",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:i18n_behavior",
    "//ui/webui/resources/js:assert.m",
  ]
  externs_list = [
    "$externs_path/chrome_extensions.js",
    "$externs_path/webview_tag.js",
  ]
}

js_library("button_bar") {
  deps = [
    ":cellular_types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:i18n_behavior",
    "//ui/webui/resources/js/cr/ui:focus_without_ink_js",
  ]
}

js_library("cellular_types") {
}

js_library("cellular_setup_delegate") {
}

js_library("subflow_behavior") {
  deps = [ ":cellular_types" ]
}

js_library("psim_flow_ui") {
  deps = [
    ":cellular_setup_delegate",
    ":cellular_types",
    ":final_page",
    ":mojo_interface_provider",
    ":provisioning_page",
    ":setup_loading_page",
    ":subflow_behavior",
    "//chromeos/ash/services/cellular_setup/public/mojom:mojom_js_library_for_compile",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:i18n_behavior",
    "//ui/webui/resources/js:assert.m",
  ]
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_library("esim_flow_ui") {
  deps = [
    ":activation_code_page",
    ":activation_verification_page",
    ":cellular_setup_delegate",
    ":cellular_types",
    ":confirmation_code_page",
    ":esim_manager_utils",
    ":final_page",
    ":mojo_interface_provider",
    ":profile_discovery_list_item",
    ":profile_discovery_list_page",
    ":subflow_behavior",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_components/chromeos/network:cellular_utils",
    "//ui/webui/resources/cr_components/chromeos/network:mojo_interface_provider",
    "//ui/webui/resources/cr_components/chromeos/network:network_listener_behavior",
    "//ui/webui/resources/cr_elements:i18n_behavior",
    "//ui/webui/resources/js:assert.m",
  ]
  externs_list = [ "$externs_path/metrics_private.js" ]
}

js_library("cellular_eid_dialog") {
  deps = [ "//ui/webui/resources/cr_elements:i18n_behavior" ]
}

js_library("cellular_setup") {
  deps = [
    ":button_bar",
    ":cellular_setup_delegate",
    ":cellular_types",
    ":esim_flow_ui",
    ":psim_flow_ui",
    "//ui/webui/resources/cr_components/chromeos/network:cellular_utils",
    "//ui/webui/resources/cr_components/chromeos/network:mojo_interface_provider",
    "//ui/webui/resources/cr_elements:i18n_behavior",
  ]
}

html_to_js("web_components") {
  js_files = [
    "activation_code_page.js",
    "activation_verification_page.js",
    "base_page.js",
    "button_bar.js",
    "cellular_eid_dialog.js",
    "cellular_setup_icons.js",
    "cellular_setup.js",
    "confirmation_code_page.js",
    "esim_flow_ui.js",
    "final_page.js",
    "profile_discovery_list_item.js",
    "profile_discovery_list_page.js",
    "provisioning_page.js",
    "psim_flow_ui.js",
    "setup_loading_page.js",
  ]
}
