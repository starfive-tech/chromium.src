# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//ui/webui/resources/include_polymer.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("../../cr.gni")

preprocess_folder = "$root_gen_dir/ui/webui/resources/preprocessed/js/cr/ui"
if (is_chromeos_ash) {
  preprocess_src_manifest = "preprocessed_src_manifest.json"
}

group("preprocess") {
  public_deps = [ ":preprocess_src_ts" ]
  if (is_chromeos_ash) {
    public_deps += [ ":preprocess_src" ]
  }
}

# TS files are passed to a separate target so that they are not listed in the
# |out_manifest|.
preprocess_if_expr("preprocess_src_ts") {
  in_folder = "."
  out_folder = preprocess_folder
  in_files = [
    "drag_wrapper.ts",
    "focus_grid.ts",
    "focus_outline_manager.ts",
    "focus_row.ts",
    "keyboard_shortcut_list.ts",
    "store_ts.ts",
  ]

  if (include_polymer) {
    in_files += [
      "focus_row_mixin.ts",
      "focus_without_ink.ts",
    ]
  }
}

# JS files for Chrome OS only.
if (is_chromeos_ash) {
  generate_grd("build_grdp") {
    grd_prefix = "webui_js_cr_ui"
    out_grd = "$target_gen_dir/resources.grdp"
    public_deps = [ ":preprocess_src" ]
    manifest_files = [ "$target_gen_dir/$preprocess_src_manifest" ]
    resource_path_prefix = "js/cr/ui"
  }

  preprocess_if_expr("preprocess_src") {
    in_folder = "./"
    out_folder = "$preprocess_folder"
    out_manifest = "$target_gen_dir/$preprocess_src_manifest"

    in_files = [
      "focus_without_ink_js.js",
      "focus_row_behavior.js",
      "focus_row_js.js",
      "keyboard_shortcut_list_js.js",
      "store_client.js",
      "store.js",
    ]
  }

  js_type_check("closure_compile_modules") {
    is_polymer3 = true
    deps = [
      ":focus_row_behavior",
      ":focus_row_js",
      ":focus_without_ink_js",
      ":keyboard_shortcut_list_js",
      ":store",
      ":store_client",
    ]
  }

  js_library("focus_row_js") {
    deps = [
      "../..:assert.m",
      "../..:event_tracker",
      "../..:util.m",
    ]
  }

  js_library("focus_row_behavior") {
    deps = [
      ":focus_row_js",
      ":focus_without_ink_js",
      "../..:assert.m",
      "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    ]
  }

  js_library("focus_without_ink_js") {
    deps = [
      "../..:assert.m",
      "../..:cr.m",
    ]
  }

  js_library("keyboard_shortcut_list_js") {
  }

  js_library("store") {
  }

  js_library("store_client") {
    deps = [
      ":store",
      "../..:assert.m",
    ]
  }
}
